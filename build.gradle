allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

    group 'com.worldline.sips'
    version '1.4.5-SNAPSHOT'

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'SIPS Payment SDK'
                    description = 'This library simplifies the use of SIPS 2.0, the Worldline e-payments API'
                    url = 'https://github.com/worldline/sips-sdk-java/'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'Support SIPS Benelux'
                            email = 'supportsips-benelux@wordline.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/worldline/sips-sdk-java'
                        connection = 'scm:git:git://github.com/worldline/sips-sdk-java.git'
                        developerConnection = 'scm:git:ssh://git@github.com:worldline/sips-sdk-java.git'
                    }
                    organization {
                        name = 'Worldline'
                        url = 'https://worldline.com'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/worldline/sips-sdk-java/issues'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://nexus.kazan.myworldline.com/repository/rcs-store-releases"
                def snapshotsRepoUrl = "https://nexus.kazan.myworldline.com/repository/rcs-store-snapshots"

                url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
                credentials {
                    username= findProperty('nexusRepoUser')
                    password= findProperty('nexusRepoPass')
                }
            }
        }
    }
//
//    signing {
//        sign publishing.publications.maven
//    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }
}
